s/\(\y\(access_\)\?token\|api_key\)=[^& "\]*/\1=[FILTERED]/g
s/applications\/\([a-zA-Z0-9]\+\)\/\(tokens\|grants\)\/[a-zA-Z0-9]\+/applications\/\1\/\2\/[FILTERED]/g
s/client_secret\(=\|"=>"\)[a-zA-Z0-9]\+/client_secret\1[FILTERED]/g
s/\(body\|request_body\|old_body\)":".\+\?"\(,"\|}[,}]\|}\$\)/\1":"FILTERED"\2/g
s@\(AccountKey\|SecretAccessKey\)=[A-Za-z0-9+/=]\+@\1=[FILTERED]@g
s/\(SharedAccessSignature\=.\+\?sig\=\)[A-Za-z0-9\%]\+/\1[FILTERED]/g
s/\(ghe-config[[:space:]]\+secrets\.[^\S]\+[[:space:]]\+\).*$/\1[FILTERED]/g
s@\(hooks.slack.com/\(services\|workflows\)/[[:alnum:]]\{9,\}/[[:alnum:]]\{9,\}/\([[:digit:]]\{12,\}/\)\?\)\([[:alnum:]]\{10\}\)\([[:alnum:]]\{14\}\)@\1[FILTERED]\5@g

#This is a special case for gh es access set-password 
s/\(--password\(-confirmation\)\?[= ]\+\)\("\([^"]*\)"\|'\([^']*\)'\|[^ ]\+\)/\1\[FILTERED\]/g

#These are the special cases for ghe-set-password. 1st one cover echo "password" | ghe-set-password and 2nd one cover ghe-set-password <<< "password"
s/echo \("\([^"]*\)"\|'\([^']*\)'\|[^ ]\+\)[[:space:]]\?\(|\)\+[[:space:]]\?\(sudo \)\?ghe-set-password/echo [FILTERED] | ghe-set-password/g
s/ghe-set-password\( \)\?<<<\( \)\?\("\([^"]*\)"\|'\([^']*\)'\|[^ ]\+\)/ghe-set-password <<< [FILTERED]/g

# General curl authorization filtering
/curl/ s/\(Authorization: \)[^"']*/\1[FILTERED]/g
/curl/ s/\(http:\/\/\|https:\/\/\)[^@]*\(@\)/\1[FILTERED]\2/g
